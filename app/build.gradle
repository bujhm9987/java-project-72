plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.adarshr.test-logger' version '3.2.0'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'io.ebean' version '13.20.1'
    id 'jacoco'
}

group = 'hexlet.code'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets.main.java.srcDirs = ['src/main/java', 'build/generated/sources/annotationProcessor/java/main']

dependencies {
    implementation 'io.javalin:javalin:5.6.1'
    implementation 'io.javalin:javalin-rendering:5.6.1'
    implementation 'org.slf4j:slf4j-simple:2.0.7'

    implementation 'org.thymeleaf:thymeleaf:3.1.2.RELEASE'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:3.2.0'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-java8time:3.0.4.RELEASE'
    implementation 'org.webjars:bootstrap:5.2.3'

    implementation 'com.h2database:h2:2.1.214'
    implementation 'org.postgresql:postgresql:42.5.4'

    implementation 'io.ebean:ebean:13.20.1'
    implementation 'io.ebean:ebean-ddl-generator:13.20.1'
    implementation 'io.ebean:ebean-querybean:13.20.1'
    implementation 'io.ebean:ebean-migration:13.9.0'
    implementation 'io.ebean:ebean-annotation:8.4'
    annotationProcessor 'io.ebean:querybean-generator:13.20.1'

    // NOTE: ebean-ddl-generator требует старые зависимости, которых нет в java 9 и выше
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.5'
    implementation 'javax.activation:activation:1.1.1'

    implementation 'com.konghq:unirest-java:3.14.5'

    implementation 'org.jsoup:jsoup:1.16.1'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'io.ebean:ebean-test:13.20.1'
    testImplementation 'com.konghq:unirest-java:3.14.5'

    testImplementation 'com.squareup.okhttp3:mockwebserver:4.10.0'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

testlogger {
    showStandardStreams true
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

checkstyle {
    toolVersion = '10.12.2'
}

compileJava {
    options.release = 20
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'hexlet.code.App'
}

task stage(dependsOn: ['clean', 'installDist'])
installDist.mustRunAfter clean

task generateMigrations(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'hexlet.code.MigrationGenerator'
}